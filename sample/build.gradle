apply plugin: 'com.android.application'

apply from: '../config/quality.gradle'

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.2'

    signingConfigs {
        config {
            keyAlias 'android'
            keyPassword 'android'
            storeFile file('keystore/release.jks')
            storePassword 'android'
        }
    }

    defaultConfig {
        applicationId "net.derohimat.samplebasemvp"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 6
        versionName "0.6"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            signingConfig signingConfigs.config
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'LICENSE.txt'
    }

    lintOptions {
        warning 'InvalidPackage'
        abortOnError false
        lintConfig file("${project.rootDir}/config/quality/lint/lint.xml")
    }
}

dependencies {
    final RETROFIT_VERSION = '2.3.0'
    final OKHTTP_VERSION = '3.8.0'
    final DAGGER_VERSION = '2.10'

    compile project(':library')
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    //----- Dagger
    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    annotationProcessor "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    provided 'org.glassfish:javax.annotation:10.0-b28' //Required by Dagger2

    //----- Retrofit
    compile("com.squareup.retrofit2:retrofit:$RETROFIT_VERSION") {
        // exclude Retrofitâ€™s OkHttp peer-dependency module and define your own module import
        exclude module: 'okhttp'
    }
    compile "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:adapter-rxjava:$RETROFIT_VERSION"
    compile "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    compile "com.squareup.okhttp3:logging-interceptor:$OKHTTP_VERSION"

    //----- The core of Reactive Programming
    compile 'io.reactivex:rxjava:1.3.0'
    compile 'io.reactivex:rxandroid:1.2.1'

    //----- Eventbuss
    compile 'org.greenrobot:eventbus:3.0.0'

    //----- Easy Permission Management
    compile "com.karumi:dexter:4.1.1"

    //----- Image Utils
    compile 'com.github.bumptech.glide:glide:3.7.0'
    //----- RecyclerView
    compile 'com.jcodecraeer:xrecyclerview:1.3.2'

    //----- Testing
    testCompile 'junit:junit:4.12'
    annotationProcessor "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    androidTestCompile 'com.android.support:support-annotations:25.4.0'
    androidTestCompile "com.android.support.test:runner:1.0.1"
    androidTestCompile "com.android.support.test:rules:1.0.1"
    androidTestCompile "com.android.support.test.espresso:espresso-core:3.0.1"
    androidTestCompile "org.hamcrest:hamcrest-library:1.3"
}

// Log out test results to console
tasks.matching {it instanceof Test}.all {
    testLogging.events = ["failed", "passed", "skipped"]
}